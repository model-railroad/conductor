# throttles

Throttle CONSIST = 204
Throttle REAR  = 209

# states

Var PA-Enable       = 1
Var PA-Half-Speed   = 8
Var PA-Summit-Speed   = 12
Var PA-Sonora-Speed   = 12
Var PA-Full-Speed   = 20

# sensors

Sensor B360         = NS768      # 49:1
Sensor B370         = NS769      # 49:2

Sensor PA-Toggle    = NS784      # 50:1
Sensor PA-Run3      = NS785      # 50:2
Sensor P3.1         = NS786      # 50:3, B503a
Sensor P3.2         = NS787      # 50:4, B503b

# turnouts

Turnout T330        = NT330

# define timers and their default value in seconds

Timer PA-Timer-370      = 19
Timer PA-Timer-370-Stop = 6
Timer PA-Timer-Summit = 8
Timer PA-Timer-P3.1   = 5
Timer PA-Timer-P3.2   = 5
Timer PA-Timer-Enable = 3
Timer PA-Timer-MidStation = 43
Timer PA-Timer-MidStationRev = 55
Timer PA-Timer-FullSpeedRev = 15
Timer PA-Timer-SonoraSpeed = 30
Timer PA-Timer-BeforeTunnel = 20
Timer PA-Timer-AfterTunnel = 50


# ---------
# Events PA
# ---------

# Setup
 PA-Toggle                            -> CONSIST Sound = 1; CONSIST Light = 1; REAR Sound = 1; REAR Light = 1; PA-Enable = 1 ; CONSIST Horn
 PA-Toggle & !PA-Enable  & PA-Run3    -> CONSIST Horn

# PA-Toggle   -> T503 = Normal;  
# PA-Toggle   -> T330 = Reverse; 

# Departure
 P3.2 & CONSIST Stopped & PA-Toggle & PA-Enable & PA-Run3 -> PA-Enable = 0; CONSIST Light = 1; CONSIST Sound = 1; REAR Light = 1 ; REAR Sound = 1; CONSIST Horn; CONSIST Forward = PA-Half-Speed 
 P3.1 & CONSIST Forward                    -> CONSIST Forward = PA-Full-Speed ; PA-Timer-MidStation Start

# Mid-Station
PA-Timer-MidStation    & CONSIST Forward -> End PA-Timer-MidStation End     ; CONSIST Horn ; CONSIST Forward = PA-Sonora-Speed ; PA-Timer-SonoraSpeed Start; PA-Timer-BeforeTunnel Start ; PA-Timer-AfterTunnel Start
PA-Timer-MidStationRev & CONSIST Reverse -> PA-Timer-MidStationRev End ; CONSIST Horn
PA-Timer-BeforeTunnel  & CONSIST Forward -> PA-Timer-BeforeTunnel End  ; CONSIST Horn
PA-Timer-AfterTunnel   & CONSIST Forward -> PA-Timer-AfterTunnel End ; CONSIST Horn
PA-Timer-SonoraSpeed   & CONSIST Forward -> PA-Timer-SonoraSpeed End ; CONSIST Forward = PA-Full-Speed

# Summit
 B360 & B370 & CONSIST Forward                    -> PA-Timer-370 Start
 PA-Timer-370                               -> PA-Timer-370 End ; CONSIST Forward = PA-Summit-Speed; PA-Timer-370-Stop Start
 PA-Timer-370-Stop                         -> PA-Timer-370-Stop End ; CONSIST Stop; PA-Timer-Summit Start ; CONSIST Horn
 PA-Timer-Summit                      -> PA-Timer-Summit End ; CONSIST Horn; CONSIST Reverse = PA-Summit-Speed ;
 B360 & CONSIST Reverse                    -> PA-Timer-FullSpeedRev Start
 PA-Timer-FullSpeedRev -> PA-Timer-FullSpeedRev End ; CONSIST Reverse = PA-Full-speed ; PA-Timer-MidStationRev Start

# Arrival
 P3.1 & CONSIST Reverse                    -> PA-Timer-P3.1 Start
 PA-Timer-P3.1 & CONSIST Reverse            -> PA-Timer-P3.1 End ; CONSIST Reverse = PA-Half-speed ;
 P3.2 & CONSIST Reverse                    -> PA-Timer-P3.2 Start
 PA-Timer-P3.2 & CONSIST Reverse            -> PA-Timer-P3.2 End ; CONSIST Stop ; CONSIST Horn; REAR Light = 0; PA-Timer-Enable Start # at station
 PA-Timer-Enable & CONSIST Stop            -> PA-Timer-Enable End ; PA-Enable = 1; CONSIST Horn

# Teardown
!PA-Toggle & !CONSIST Reverse & !P3.2      -> CONSIST Reverse = PA-Full-Speed ; 
!PA-Toggle &  CONSIST Stopped              -> CONSIST Sound = 0 ; REAR Sound = 0;


