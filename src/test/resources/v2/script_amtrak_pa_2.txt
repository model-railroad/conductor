# throttles

Throttle CONSIST = 204
Throttle REAR  = 209

# states

Var PA-Enable           = 1
Var PA-Station-Speed    = 8
Var PA-Summit-Speed     = 12
Var PA-Sonora-Speed     = 12
Var PA-Full-Speed       = 20

# sensors

Sensor B360         = NS768      # 49:1
Sensor B370         = NS769      # 49:2

Sensor PA-Toggle    = NS784      # 50:1
Sensor PA-Run3      = NS785      # 50:2
Sensor P3.1         = NS786      # 50:3, B503a
Sensor P3.2         = NS787      # 50:4, B503b

# turnouts

Turnout T330        = NT330

# define timers and their default value in seconds

Timer PA-Timer-Summit = 8


# ---------
# Events PA
# ---------

# Setup
 PA-Toggle                            -> CONSIST Sound = 1; CONSIST Light = 1; REAR Sound = 1; REAR Light = 1; PA-Enable = 1 ; CONSIST Horn
 PA-Toggle & !PA-Enable  & PA-Run3    -> CONSIST Horn

# PA-Toggle   -> T503 = Normal;  
# PA-Toggle   -> T330 = Reverse; 

# Departure
 P3.2      & CONSIST Stopped & PA-Toggle & PA-Enable & PA-Run3 -> PA-Enable = 0; CONSIST Light = 1; CONSIST Sound = 1; REAR Light = 1 ; REAR Sound = 1; CONSIST Horn; CONSIST Forward = PA-Station-Speed
 P3.1      & CONSIST Forward    -> CONSIST Forward = PA-Full-Speed
# Sonora speed reduction
 P3.1 + 43 & CONSIST Forward    -> CONSIST Forward = PA-Sonora-Speed
 P3.1 + 73 & CONSIST Forward    -> CONSIST Forward = PA-Full-Speed
# Mid-Station on the way up
 P3.1 + 63 & CONSIST Forward    -> CONSIST F3
 P3.1 + 93 & CONSIST Forward    -> CONSIST F3


# Summit
 B370 +  9 & CONSIST Forward    -> CONSIST Forward = PA-Summit-Speed
 B370 + 15 & CONSIST Forward    -> CONSIST Stop; PA-Timer-Summit Start ; CONSIST Horn
 PA-Timer-Summit                -> CONSIST Horn; CONSIST Reverse = PA-Summit-Speed
 !B370 + 10 & CONSIST Reverse   -> CONSIST Reverse = PA-Full-speed
# Mid-Station on the way down
 !B370 + 65 & CONSIST Reverse   -> CONSIST F3


# Arrival at station
 P3.1 +  5 & CONSIST Reverse    -> CONSIST Reverse = PA-Station-Speed
 P3.2 + 15 & CONSIST Reverse    -> CONSIST Stop; CONSIST Horn; REAR Light = 0
 P3.2 + 18 & CONSIST Stopped    -> PA-Enable = 1; CONSIST Horn

# Teardown
!PA-Toggle & !CONSIST Reverse & !P3.2   -> CONSIST Reverse = PA-Full-Speed ;
!PA-Toggle &  CONSIST Stopped           -> CONSIST Sound = 0 ; REAR Sound = 0;


