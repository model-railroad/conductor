# throttles

Throttle FRONT = 204
Throttle REAR  = 209

# states

Var PA-Enable       = 1
Var PA-Half-Speed   = 8
Var PA-Summit-Speed   = 12
Var PA-Full-Speed   = 20

# sensors

Sensor B360         = NS768      # 49:1
Sensor B370         = NS769      # 49:2

Sensor PA-Toggle    = NS784      # 50:1
Sensor PA-Run3      = NS785      # 50:2
Sensor P3.1         = NS786      # 50:3, B503a
Sensor P3.2         = NS787      # 50:4, B503b

# turnouts

Turnout T330        = NT330

# define timers and their default value in seconds

Timer PA-Timer-370      = 9
Timer PA-Timer-370-Stop = 6
Timer PA-Timer-Summit = 8
Timer PA-Timer-P3.1   = 5
Timer PA-Timer-P3.2   = 15
Timer PA-Timer-Enable = 3
Timer PA-Timer-MidStation = 37
Timer PA-Timer-MidStationRev = 64
Timer PA-Timer-Tunnel = 72



# ---------
# Events PA
# ---------

# Setup
 PA-Toggle                            -> FRONT Sound = 1; FRONT Light = 1; REAR Sound = 1; REAR Light = 1; PA-Enable = 1 ; FRONT Horn
 PA-Toggle + !PA-Enable  + PA-Run3    -> FRONT Horn

# PA-Toggle   -> T503 = Normal;  
# PA-Toggle   -> T330 = Reverse; 

# Departure
 P3.2 + FRONT Stopped + PA-Toggle + PA-Enable + PA-Run3 -> PA-Enable = 0; FRONT Light = 1; FRONT Sound = 1; REAR Light = 1 ; REAR Sound = 1; FRONT Horn; FRONT Forward = PA-Half-Speed ; REAR Reverse = PA-Half-Speed
 P3.1 + FRONT Forward                    -> FRONT Forward = PA-Full-Speed ; REAR Reverse = PA-Full-Speed ; Start = PA-Timer-MidStation ; Start = PA-Timer-Tunnel

# Mid-Station
PA-Timer-MidStation    + FRONT Forward -> End = PA-Timer-MidStation    ; FRONT Horn
PA-Timer-MidStationRev + FRONT Reverse -> End = PA-Timer-MidStationRev ; FRONT Horn
PA-Timer-Tunnel        + FRONT Forward -> End = PA-Timer-Tunnel        ; FRONT Horn

# Summit
 B370 + FRONT Forward                    -> Start = PA-Timer-370
 PA-Timer-370                         -> End = PA-Timer-370; FRONT Forward = PA-Summit-Speed; REAR Reverse = PA-Summit-Speed ; Start = PA-Timer-370-Stop
 PA-Timer-370-Stop                         -> End = PA-Timer-370; FRONT Stop; REAR Stop ; Start = PA-Timer-Summit; FRONT Horn
 PA-Timer-Summit                      -> End = PA-Timer-Summit ; FRONT Horn; FRONT Reverse = PA-Summit-Speed ; REAR Forward = PA-Summit-Speed
 B360 + FRONT Reverse                    -> FRONT Reverse = PA-Full-speed ; REAR Forward = PA-Full-Speed ; Start = PA-Timer-MidStationRev

# Arrival
 P3.1 + FRONT Reverse                    -> Start = PA-Timer-P3.1
 PA-Timer-P3.1                           -> End = PA-Timer-P3.1 ; FRONT Reverse = PA-Half-speed ; REAR Forward = PA-Half-Speed
 P3.2 + FRONT Reverse                    -> Start = PA-Timer-P3.2
 PA-Timer-P3.2                        -> End = PA-Timer-P3.2; FRONT Stop ; REAR Stop ; FRONT Horn; REAR Light = 0; Start = PA-Timer-Enable # at station
 PA-Timer-Enable                      -> End = PA-Timer-Enable; PA-Enable = 1; FRONT Horn

# Teardown
!PA-Toggle + !FRONT Reverse + !P3.2      -> FRONT Reverse = PA-Full-Speed ; REAR Forward = PA-Full-Speed
!PA-Toggle +  FRONT Stopped              -> FRONT Sound = 0 ; REAR Sound = 0;


