/*
 * Project: RTAC
 * Copyright (C) 2017 alf.labs gmail com,
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: "com.android.application"

android {
    namespace "com.alflabs.rtac"

    compileSdkVersion 34

    defaultConfig {
        applicationId "com.alflabs.rtac"

        // Hard-requirement: Hi10 Pro ⇒ Android 5.1 ⇒ API 22 (Lollipop MR1)
        minSdkVersion 21

        targetSdkVersion 34
        versionCode 1006
        versionName "1.06"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    // Fixed build error : Duplicate files copied in APK META-INF/xxx [https://stackoverflow.com/questions/20827885]
    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LICENSE.txt"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(":LibManifest")
    implementation project(":LibUtilsJava")
    implementation project(":LibUtilsAndroid")
    implementation project(":androidsvg")

    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7.1"
    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "androidx.fragment:fragment:1.6.2"
    implementation "com.google.guava:guava:28.1-android"

    // Note: annotationProcessor starting with gradle 2.2 replaces the previous APT (annotation processor plugin)
    def dagger_version = "2.49"
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Testing: Robolectric, Mockito and Truth
    testImplementation "org.mockito:mockito-core:5.4.0"
    testImplementation "org.robolectric:robolectric:4.7.1"
    testImplementation "com.google.truth:truth:1.1.5"
    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.guava:guava:28.1-jre"

    //--not used--
    // compile fileTree(dir: "libs", include: ["*.jar"])
    // androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
    //     exclude group: "com.android.support", module: "support-annotations"
    // })

    // https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
    // To find the version of kotlin, run $ ./gradlew :app:dependencies | grep kotlin
    // Some androidx libs use 1.8.10 and some use 1.6.21, which creates duplicate conflicts.
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.10") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

apply from: "test_resources.gradle"
