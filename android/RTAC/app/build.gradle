apply plugin: "com.android.application"

android {
    compileSdkVersion 26

    if (!gradle.gradleVersion.startsWith("4")) {
        buildToolsVersion "25.0.2"      // [AS2]
    } else {
        buildToolsVersion "26.0.0"      // [AS3]
    }

    defaultConfig {
        applicationId "com.alflabs.rtac"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (!gradle.gradleVersion.startsWith("4")) {
            // Needed to use Java 1.8 features such as lambdas and and streams [AS2]
            jackOptions {
                enabled !gradle.gradleVersion.startsWith("4")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    // Fixed build error : Duplicate files copied in APK META-INF/xxx [https://stackoverflow.com/questions/20827885]
    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LICENSE.txt"
    }

    lintOptions {
        abortOnError false
    }

    if (!gradle.gradleVersion.startsWith("4")) {
        dexOptions {
            incremental true
        }
    }
}

dependencies {
    compile project(":LibManifest")
    compile project(":LibUtilsAndroid")

    compile "com.caverock:androidsvg:1.2.1"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7.1"

    //--compile "com.android.support:appcompat-v7:26.+"
    //--compile "com.android.support.constraint:constraint-layout:1.0.2"

    // Note: annotationProcessor starting with gradle 2.2 replaces the previous APT (annotation processor plugin)
    compile "com.google.dagger:dagger:2.5"
    annotationProcessor "com.google.dagger:dagger-compiler:2.5"

    // Testing: Robolectric, Mockito and Truth
    testCompile "org.mockito:mockito-core:2.2.9"
    testCompile "org.robolectric:robolectric:3.1" // uses hamcrest 1.3
    testCompile "com.google.truth:truth:0.34"     // to match hamcrest 1.3 from Robolectric
    testCompile "junit:junit:4.12"                // to match hamcrest 1.3 from Robolectric
    testCompile "com.google.guava:guava:20.0"

    //--not used--
    // compile fileTree(dir: "libs", include: ["*.jar"])
    // androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
    //     exclude group: "com.android.support", module: "support-annotations"
    // })
}
