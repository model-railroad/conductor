apply plugin: "com.android.application"

android {
    compileSdkVersion 26

    // [AS2] vs [AS3]
    buildToolsVersion (gradle.gradleVersion < "4" ? "25.0.2" : "26.0.0")

    defaultConfig {
        applicationId "com.alflabs.rtac"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (gradle.gradleVersion < "4") {
            // Needed to use Java 1.8 features such as lambdas and and streams [AS2]
            jackOptions {
                enabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    // Fixed build error : Duplicate files copied in APK META-INF/xxx [https://stackoverflow.com/questions/20827885]
    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LICENSE.txt"
    }

    lintOptions {
        abortOnError false
    }

    if (!gradle.gradleVersion.startsWith("4")) {
        dexOptions {
            incremental true
        }
    }
}

dependencies {
    compile project(":LibManifest")
    compile project(":LibUtilsJava")
    compile project(":LibUtilsAndroid")
    compile project(":androidsvg")

    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7.1"

    // Note: annotationProcessor starting with gradle 2.2 replaces the previous APT (annotation processor plugin)
    compile "com.google.dagger:dagger:2.5"
    annotationProcessor "com.google.dagger:dagger-compiler:2.5"

    // Testing: Robolectric, Mockito and Truth
    testCompile "org.mockito:mockito-core:2.2.9"
    testCompile "org.robolectric:robolectric:3.1" // uses hamcrest 1.3
    testCompile "com.google.truth:truth:0.34"     // to match hamcrest 1.3 from Robolectric
    testCompile "junit:junit:4.12"                // to match hamcrest 1.3 from Robolectric
    testCompile "com.google.guava:guava:20.0"

    //--not used--
    // compile fileTree(dir: "libs", include: ["*.jar"])
    // androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
    //     exclude group: "com.android.support", module: "support-annotations"
    // })
}

apply from: "test_resources.gradle"
