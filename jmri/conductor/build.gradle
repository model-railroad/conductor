plugins {
    id "java"
    id "idea"
    // Add the Annotation Processor Plugin to run dagger at build-time.
    id "net.ltgt.apt" version "0.10"
}

group "com.alflabs.conductor"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

configurations {
    antlr4 {
        description = "ANTLR4"
    }
}

ext.antlr = [
        antlrDestPkg: "com.alflabs.conductor.parser2",
        antlrSrcDir : "src/antlr/antlr",
        antlrGenDir : "src/antlr/java"
]

ext.dagger = [
    generatedSrcDir: "src/dagger/java"
]

sourceSets {
    // Gradle syntax to add source dirs is either:
    // All in one line to set a full new srcDir set:
    //      main.java.srcDir [dirToAdd]+
    // or to extend the block using one srcDirs added per line:
    //      main { java { [srcDirs dirToAdd]+ } }
    // (srcDir sets a list whereas srcDirs adds to a list)

    main {
        java {
            srcDirs antlr.antlrGenDir
        }
        output.generatedSourcesDir = dagger.generatedSrcDir
    }
}

dependencies {
    compile project(":LibManifest")
    compile project(":LibUtilsJava")
    compile "com.google.guava:guava:19.0"
    compile "com.intellij:forms_rt:7.0.3"
    compile "com.google.truth:truth:0.31"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7.1"

    // antlr4 dep for building g4 to java sources, and antlr runtime for compiling generated srcs
    antlr4  group: "org.antlr", name: "antlr4",         version: "4.5.3"
    compile group: "org.antlr", name: "antlr4-runtime", version: "4.5.3"

    compile "com.google.dagger:dagger:2.11"
    apt     "com.google.dagger:dagger-compiler:2.11"

    compile "com.google.auto.factory:auto-factory:1.0-beta5"
    apt     "com.google.auto.factory:auto-factory:1.0-beta5"

    testCompile "junit:junit:4.10"
    testCompile "com.google.truth:truth:0.31"
    testCompile "org.mockito:mockito-core:2.2.9"
    testCompile "com.google.guava:guava:19.0"
}

jar {
    manifest {
        attributes(
            "Main-Class": "com.alflabs.conductor.EntryPoint",
        )
    }
}

// Build a "fat" jar with all the dependencies. C.f. http://stackoverflow.com/questions/4871656
task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = "all"
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task mkAntlrOutputDir {
    mkdir(antlr.antlrGenDir)
}

task genAntlrSources(dependsOn: mkAntlrOutputDir, type: JavaExec) {
    // Based on https://github.com/xoom/gradle-antlr4-template/blob/master/build.gradle
    description = "Generates Java sources from an ANTLR4 grammar."

    inputs.dir file(antlr.antlrSrcDir)
    outputs.dir file(antlr.antlrGenDir)

    logger.info("ANTLR4 inputs: " + inputs)
    logger.info("ANTLR4 outputs: " + outputs)

    def grammars = fileTree(antlr.antlrSrcDir).include("**/*.g4")

    main = "org.antlr.v4.Tool"
    classpath = configurations.antlr4
    def pkg = antlr.antlrDestPkg.replaceAll("\\.", "/")
    args = [ "-o",
             "${antlr.antlrGenDir}/${pkg}",
             /*"-atn", */
             "-listener",
             "-no-visitor",
             "-package", antlr.antlrDestPkg,
             grammars.files
    ].flatten()
}

//noinspection GroovyAssignabilityCheck
task adjustAntlrGenDir(dependsOn: genAntlrSources) {
    description = "Adjusts files generated by ANTLR4 compilation."

    def pattern = ~/(\/\/ Generated from ).+[\/\\]([A-Za-z]+.g4 by ANTLR .*)/

    fileTree(antlr.antlrGenDir).include("**/*").each { f ->
        logger.info("Checking file $f")
        def r = new BufferedReader(new InputStreamReader(new FileInputStream(f), "UTF-8"))
        ArrayList<String> lines = new ArrayList()
        r.eachLine { line ->
            def m1 = pattern.matcher(line)
            if (m1.matches()) {
                def line2 = m1[0][1] + m1[0][2]
                if (line != line2) {
                    line = line2
                }
            }

            lines.add(line)
        }
        r.close()

        logger.info("Rewrite file $f")

        // Force the file to use LF instead of CRLF on Windows
        def w = new FileOutputStream(f)
        lines.each { line ->
            w.write(line.getBytes("UTF-8"))
            w.write((byte)'\n')
        }
        w.close()
    }
}

compileJava {
    dependsOn adjustAntlrGenDir
    delete dagger.generatedSrcDir
    source antlr.antlrGenDir
    source dagger.generatedSrcDir
}

clean {
    delete antlr.antlrGenDir
    delete dagger.generatedSrcDir
}

artifacts {
    archives fatJar
}

