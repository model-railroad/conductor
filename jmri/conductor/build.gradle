/*
 * Project: Conductor
 * Copyright (C) 2017 alf.labs gmail com,
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// ----------
// IMPORTANT: all plugin/deps VERSIONS are defined in gradle.properties.
// ----------

apply plugin: "java"
apply plugin: "idea"

group   artifact_group
version artifact_vers

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":common")
    implementation project(":engine1")
    implementation project(":engine2k")
    implementation project(":ui2")
    implementation project(":simul2k")
    implementation project(":LibManifest")
    implementation project(":LibUtilsJava")

    implementation "com.google.guava:guava:$vers_guava"
    implementation "com.intellij:forms_rt:$vers_forms_rt"
    implementation "com.google.truth:truth:$vers_truth"
    implementation "com.fasterxml.jackson.core:jackson-databind:$vers_jackson"
    implementation "org.jmdns:jmdns:$vers_jmdsn"
    implementation "org.slf4j:slf4j-simple:$vers_sl4j"     // used by jmdns
    implementation "com.squareup.okhttp3:okhttp:$vers_okhttp"
    implementation "org.codehaus.groovy:groovy-all:$vers_groovy"

    // For Kotlin KTS and Scripting Host:
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$vers_kotlin"
    implementation "org.jetbrains.kotlin:kotlin-scripting-common:$vers_kotlin"

    implementation          "com.google.dagger:dagger:$vers_dagger"
    annotationProcessor     "com.google.dagger:dagger-compiler:$vers_dagger"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$vers_dagger"

    implementation      "com.google.auto.factory:auto-factory:$vers_autofactory"
    annotationProcessor "com.google.auto.factory:auto-factory:$vers_autofactory"

    testImplementation testFixtures(project(":common"))
    testImplementation testFixtures(project(":engine1"))
    testImplementation "junit:junit:$vers_junit"
    testImplementation "com.google.truth:truth:$vers_truth"
    testImplementation "org.mockito:mockito-core:$vers_mockito"
    testImplementation "com.google.guava:guava:$vers_guava"
}

jar {
    manifest {
        attributes(
            "Main-Class": "com.alflabs.conductor.EntryPoint",
        )
    }
}

// Build a "fat" jar with all the dependencies. C.f. http://stackoverflow.com/questions/4871656
task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.from jar.manifest
    classifier = "all"
    from {
        configurations.default.collect {
            // println "fatJar: packaging " + it.name
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

artifacts {
    archives fatJar
}

